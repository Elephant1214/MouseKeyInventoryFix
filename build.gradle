buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://maven.parchmentmc.org" }
        maven { url = "https://repo.spongepowered.org/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath group: "org.parchmentmc", name: "librarian", version: "1.+", changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7.+"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.parchmentmc.librarian.forgegradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "idea"

String modid = "mousekeyinventoryfix"
version = "Forge1.16.x-1.0.0"
group = "nathan.${modid}"
archivesBaseName = "MouseKeyInventoryFix"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))
minecraft {
    mappings channel: "parchment", version: "2022.03.06-1.16.5"
    // makeObfSourceJar = false

    runs {
        client {
            workingDirectory project.file("run")
            arg "-mixin.config=${modid}.mixins.json"

            mods {
                mousekeyinventoryfix {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")

            mods {
                mousekeyinventoryfix {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            args "--mod", "${modid}", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")

            mods {
                mousekeyinventoryfix {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }

dependencies {
    minecraft "net.minecraftforge:forge:1.16.5-36.2.39"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
    config "${modid}.mixins.json"
}

jar {
    manifest {
        attributes([
                "TweakClass"  : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"  : "0",
                "MixinConfigs": "${modid}.mixins.json",
                "MixinConnector": "nathan.${modid}.mixin.MixinConnector"
        ])
    }
}

jar.finalizedBy("reobfJar")
